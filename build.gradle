plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.jonroler'
version = '1.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'LongPowerSet'
                description = 'A Java library for working with power sets that can handle sets with up to 62 elements'
                url = 'https://github.com/jonroler/longpowerset'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'jonroler'
                        name = 'Jon Oler'
                        email = 'jonroler@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/jonroler/longpowerset.git'
                    developerConnection = 'scm:git:ssh://github.com:jonroler/longpowerset.git'
                    url = 'https://github.com/jonroler/longpowerset'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'staging'
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
